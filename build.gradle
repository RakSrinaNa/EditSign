buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("com.google.guava:guava:30.1.1-jre")
        classpath("commons-io:commons-io:2.9.0")
    }
}

import com.modrinth.minotaur.TaskModrinthUpload
import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.jetbrains.changelog.ExtensionsKt

plugins {
    id("com.matthewprenger.cursegradle") version("1.4.0")
    id("com.modrinth.minotaur") version("1.2.0")
    id("com.github.breadmoirai.github-release") version("2.2.12")
    id("com.github.ben-manes.versions") version("0.38.0")
    id("org.jetbrains.changelog") version("1.1.2")
}

allprojects {
    apply plugin: "java"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs = ["-Xlint:deprecation"]
    }
}

group = "fr.raksrinana"
archivesBaseName = "${project.modName}-${project.minecraft_version}"

task buildJar(type: Jar, group: "modding") {
    dependsOn ":common:jar", ":fabric:build", ":forge:build"
    finalizedBy ":forge:signJar"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from zipTree("common/build/libs/common-${project.version}.jar"),
            zipTree("fabric/build/libs/fabric-${project.version}.jar"),
            zipTree("forge/build/libs/forge-${project.version}.jar")

    manifest {
        attributes([
                "Specification-Title"     : project.modId,
                "Specification-Vendor"    : "RakSrinaNa",
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.modName,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "RakSrinaNa",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
assemble.finalizedBy("buildJar")

curseforge {
    apiKey = "${project.findProperty("curseforgeApiKey")}"
    project {
        id = "${project.curseforge_project_id}"
        changelogType = "markdown"
        addGameVersion project.minecraft_version
        changelog = project.changelog.getLatest().toText()
        releaseType = "${project.releaseType}"
        mainArtifact(jar) {
            displayName = "${archivesBaseName}-${project.version}"
        }
        relations {
            //Fabric
            optionalDependency "fabric-api" //It's actually required
            optionalDependency "modmenu"
            embeddedLibrary "cloth-config"

            //Forge
            optionalDependency "cloth-config-forge"
        }
    }
    options {
        javaVersionAutoDetect = false
        javaIntegration = false
        forgeGradleIntegration = false
    }
}

task publishModrinth(type: TaskModrinthUpload, group: "upload") {
    dependsOn buildJar

    token = project.findProperty("modrinthToken") as String
    projectId = project.modrinth_project_id
    versionNumber = "${project.minecraft_version}-${project.version}"
    versionName = "${archivesBaseName}-${project.version}"
    uploadFile = jar
    addGameVersion(project.minecraft_version as String)
    addLoader("fabric")
    addLoader("forge")
    versionType = project.releaseType.toLowerCase()
    changelog = project.changelog.getLatest().toText()
}

githubRelease {
    def tag = "${project.minecraft_version}-${project.version}"

    owner = project.repoUser
    repo.set(project.repoName as String)
    token("${project.findProperty("githubReleaseToken")}")
    tagName.set(tag)
    releaseName.set(tag)
    targetCommitish.set("${project.minecraft_version}")
    body.set(project.changelog.getLatest().toText())
}

def writeUpdateJson() {
    def version = project.version
    def slurper = new JsonSlurper()
    def file = project.file("update.json")
    def json = slurper.parse(file)
    def changelogUrl = "https://github.com/${project.repoUser}/${project.repoName}/blob/${project.minecraft_version}/CHANGELOG.md"

    json["changelog"] = changelogUrl

    // TODO: Maybe add something to create an abbreviated changelog? May not be useful...
    if (!json[project.minecraft_version].containsKey(version.toString()))
        json[project.minecraft_version][version.toString()] = changelogUrl
    json[project.minecraft_version] = json[project.minecraft_version].sort([compare: { a, b -> b.compareTo(a) }] as Comparator)
    json["promos"]["${project.minecraft_version}-latest"] = version
    json["promos"]["${project.minecraft_version}-recommended"] = version

    println(json)
    file.write(JsonOutput.prettyPrint(JsonOutput.toJson(json)))
}

task createUpdateJson(group: "modding") {
    doLast {
        println("Updating update.json...")
        writeUpdateJson()
    }
}
build.finalizedBy createUpdateJson

changelog {
    version = "${project.minecraft_version}-${project.version}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = { "[${project.minecraft_version}-${project.version}] - ${ExtensionsKt.date("yyyy-MM-dd")}" }
    headerParserRegex = ~/\d+\.\d+\.\d+-\d+\.\d+\.\d+/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = ["Changed"]
}
